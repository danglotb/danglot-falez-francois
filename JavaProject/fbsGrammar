list_function			:= 	*  function

function				:= 	type "function" function_name "(" ")" "{" list_instruction "}"
						| 	type "function" function_name "(" list_parameter_init ")" "{" list_instruction "}"

list_parameter_init		:= 	parameter_init
						|	parameter_init "," list_parameter_init
						
list_parameter			:= 	parameter
						|	parameter "," list_parameter

parameter_init			:=	type variable

parameter				:=	variable
						|	number

list_instruction		:= 	* instruction
		
instruction				:=	structure
						|	singleLine_instruction ";"
						|	special_instruction ";"
						
special_instruction		:=	"display" number
						|	"display" string
						
return_instruction		:=	"return" number
						
singleLine_instruction	:=	declaration_instruction
						|	allocation_instruction
						|	call_function
						|	return_instruction

allocation_instruction 	:= 	variable "=" number
						|	variable "=" array

						
declaration_instruction := 	type allocation_instruction
						|	type variable
				
call_function			:=	function_name "(" ")"
						|	function_name "(" list_parameter ")"		

block_instruction		:=	instruction
						|	"{" list_instruction "}"

structure				:=	"while" "(" boolean_expresion ")" block_instruction
						|	structure_if	
					
structure_if			:= 	"if" "(" boolean_expresion ")" block_instruction
						|	"if" "(" boolean_expresion ")" block_instruction "else" block_instruction
						|	"if" "(" boolean_expresion ")" block_instruction "else"	structure

boolean_expresion		:= 	"(" boolean_expresion ")"
						|	boolean
						|	number op_compare number
						|	boolean_expresion op_logic boolean_expresion
							
op_compare				:= 	"<"
						|	">"
						|	"=="
						|	">="
						|	"<="
						
op_logic				:=  "&&"
						|	"||"
					
op_arithmetic			:=	"+"
						|	"-"
						|	"/"
						|	"%"
						|	"*"
						
type 					:=	"int"
						|	"float"
						|	"boolean"
						|	"void"
						|	type "[]"

boolean					:= 	"true"
						|	"false"
					
number					:=	"(" number ")"
						|	integer
						|	decimal
						|	variable
						|	call_function
						|	number op_arithmetic number
						
variable				:=	variable_name
						|	"*" variable
						|	"&" variable
						|	variable "[" number "]"

array					:=	"{" list_entities "}"
						|	"array" number
						
integer					:= [(0-9)+]

decimal					:= [(0-9)+(.)(0-9)+]

string					:= [(")(a-zA-Z0-9)*(")]

entities				:=	number
						|	string
						
list_entities			:=	list_entities "," entities
						|	entities

variable_name			:= [(a-zA-Z)(a-zA-Z0-9_)*]

function_name			:= [(a-zA-Z)(a-zA-Z0-9_)*]
